pkgname=tar
pkgver=1.35
pkgrel=2
pkgdesc='Utility used to store, backup, and transport files'
arch=('x86_64')
url='https://www.gnu.org/software/tar/'
license=('GPL3')
depends=('glibc' 'acl')
checkdepends=('attr')
options=('!emptydirs')
validpgpkeys=('41EF7182553A87AC18196A77F27F2FDA54F067D8') # Lingmo OS Team <team@lingmo.org>
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  :
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/tar \
    --enable-backup-scripts
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
